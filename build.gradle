apply from: 'dependencies.gradle'

buildscript {
    ext {
        springBootVersion = '2.0.1.RELEASE'
        springLoadedVersion = '1.2.8.RELEASE'
        kotlinVersion = "1.2.31"
        dependencyManagementVersion = "1.0.5.RELEASE"
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.springframework:springloaded:${springLoadedVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}")
    }

    repositories {
        maven {
            url = 'https://repo1.maven.org/maven2'
        }
    }
}

configure(allprojects) { project ->
    group = 'com.huijiewei'
    version = '1.18.0302'

    repositories {
        maven {
            url = "https://repo1.maven.org/maven2"
        }
        maven {
            url = 'https://repo.spring.io/libs-milestone'
        }
    }

    apply plugin: 'idea'

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/main/")
        }
    }
}

configure(subprojects) { project ->
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: "kotlin-spring"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
        compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
        kotlinOptions.freeCompilerArgs = ["-Xskip-runtime-version-check", "-Xjsr305=strict"]
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    kapt {
        useBuildCache = true
    }
}

